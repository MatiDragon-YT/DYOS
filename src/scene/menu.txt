Menu.SetActiveItem($PANEL, 0)
:MenuDeEscenarios
Menu.SetColumn($PANEL,0,'M0000Q6','M0000DU','M0000Q7','M0000TJ','M00007R','M0000M8',none,none,none,none,none,none,none)
FrenarSeleccion()
FrenarRetorno()
repeat
	Wait0()
	if EntrarAlMenu()
	then
		//{$I ../../src/const.txt}

		
		if _Selected == 0 // SAVE SCENE
		then
			Text.PrintHelpForever('M0000M7')
			{$I ./save-ini.txt}

			Text.ClearHelp()
			Aplicated()
		end

		if _Selected == 1 // LOAD SCENE
		then
			ResetCamera()
			{$I ./load-ini.txt}
		end
		
		if _Selected == 2 // Limpiar
		then
			while $numActores > 0
				Wait0()
				$indActor = $numActores
				Actor.Remove($Actor($indActor,255i))
				$numActores -= 1
			end
			while $numAutos > 0
				Wait0()
				$indAuto = $numAutos
				Car.Remove($Auto($indAuto,255i))
				$numAutos -= 1
			end
			while $numObjetos > 0
				Wait0()
				$indObjeto = $numObjetos
				
				if bit_run $statsObjet($indObjeto,255i) OBJETO_PROP_HAVE_PARTICLE
				then
					int 0@ = PARTICLE_HANDLE
					int 1@ = $indObjeto
					bget_particle()
					Particle.Remove(0@)
				end
				Object.Remove($Objeto($indObjeto,255i))
				$numObjetos -= 1
				bset_particle()
			end
			
			ResetCamera()
			Aplicated()
		end

		if _Selected == 3 // Refrescar
		then
			if $numActores > 0
			then
				int 5@ = 0
				$indActor = 0

				while $numActores > 5@
					Wait0()
					5@ += 1
					$indActor += 1

    				ResetAnimActor()

					int 0@ = B_ACTOR_REP_ANIM
					int 1@ = 5@
					bget_actor()
					int 26@ = 0@
					if 26@ >= 0
					then
						$CURRENT_ANIM_ID = 26@
						AnimDyom()
						
						if $ANIM_NOT_IS_OF_PED == false
						then
							Task.PlayAnim($Actor($indActor,255i), v$IFP_ANIM, "PED", 1000.0, true, false, false, false, -1)
						else
							Animation.Load(v$IFP_FILE)
							repeat
								Wait0()
							until Animation.Available(v$IFP_FILE)
							Task.PlayAnim($Actor($indActor,255i), v$IFP_ANIM, v$IFP_FILE, 1000.0, true, false, false, false, -1)
							Animation.Remove(v$IFP_FILE)
						end
					end

					int 0@ = B_ACTOR_GENERATION
					int 1@ = $indActor
					bget_actor()
					int 8@ = 0@

					if or
						8@ == GENERATION_ROPE_SPECIAL
						8@ == GENERATION_ROPE_CUSTOM
						8@ == GENERATION_ROPE_NORMAL
					then
						9@ = Actor.GetModel($Actor($indActor,255i))

						// COORDS
						int 0@ = B_ACTOR_LCOORD_X
						int 1@ = $indActor
						bget_actor()
						float 0@
						float 11@ = 0@// x

						int 0@ = B_ACTOR_LCOORD_Y
						int 1@ = $indActor
						bget_actor()
						float 0@
						float 12@ = 0@// y

						int 0@ = B_ACTOR_LCOORD_Z
						int 1@ = $indActor
						bget_actor() 
						float 0@
						float 13@ = 0@// z

						int 0@ = B_ACTOR_LCOORD_A
						int 1@ = $indActor
						bget_actor()
						float 0@
						float 14@ = 0@// a

						//Actor.GetPositionAndOffset(11@,12@,13@,$Actor($indActor,255i),0.0,0.0,-1.0)
						//14@ = Actor.GetAngle($Actor($indActor,255i))

						Actor.Remove($Actor($indActor,255i))

						if 8@ == GENERATION_ROPE_NORMAL
						then 
							Model.Load(9@)
							repeat
								Wait0()
							until Model.Available(9@)

							int 0@ = B_ACTOR_TYPE
							int 1@ = 5@
							bget_actor()
							int 10@ = 0@

							$Actor($indActor,255i) = Actor.CreateOnRope(10@, 9@, 11@, 12@, 13@)
							Model.Release(9@)
						else

							int 0@ = B_ACTOR_MODEL_SPECIAL
							int 1@ = $indActor
							bget_actor()
							int 15@ = 0@

							if 8@ == GENERATION_ROPE_SPECIAL
							then LoadSpecialActor()
							end

							if 8@ == GENERATION_ROPE_CUSTOM
							then call @CustomSkin4 1 15@
							end
							repeat
								Wait0()
							until SpecialActor.Available(1)
							$Actor($indActor,255i) = Actor.CreateOnRope(PedType.Special, #SPECIAL01, 11@, 12@, 13@)
							SpecialActor.Release(1)
						end
						Actor.SetAngle($Actor($indActor,255i)) = 14@
					end
				end
			end

			jump @CerrarElMenu
		end

		if _Selected == 4 // LOAD CUTSCENE
		then
			{$I ./cutscene.txt}
			Menu.SetActiveItem($PANEL,4)
			$Numero_De_Paginas = 1
			$Pagina_Actual = 1
			jump @MenuDeEscenarios
		end
		FrenarSeleccion()
	end
until SalirDelMenu()  

Menu.SetActiveItem($PANEL, menu_del_escenario)
jump @MenuPrincipal