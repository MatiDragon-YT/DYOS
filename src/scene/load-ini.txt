
//fade 0 0
if $indActor == 0
then $indActor = 1
end
while $numActores > 0
	Wait0()
	$indActor = $numActores
	Actor.Remove($Actor($indActor,255i))
	$numActores -= 1
end
while $numAutos > 0
	Wait0()
	$indAuto = $numAutos
	Car.Remove($Auto($indAuto,255i))
	$numAutos -= 1
end
while $numObjetos > 0
	Wait0()
	$indObjeto = $numObjetos

	if bit_run $statsObjet($indObjeto,255i) OBJETO_PROP_HAVE_PARTICLE
	then
		int 0@ = PARTICLE_HANDLE
		int 1@ = $indObjeto
		bget_particle()
		Particle.Remove(0@)
	end
	Object.Remove($Objeto($indObjeto,255i))
	
	$numObjetos -= 1
end

World.DestroyAllTrains
World.SetClearAreaOfCars(-3000.0, -3000.0, -100.0, 3000.0, 3000.0, 2000.0)
World.SetClearAreaOfActors(-3000.0, -3000.0, -100.0, 3000.0, 3000.0, 2000.0)

int 0@ = IniFile.GetInt("./dyos.1.ini","main","saved")
if 0@ == true
then
	float 12@ = IniFile.GetFloat("./dyos.1.ini","camera","pos.x")
	float 13@ = IniFile.GetFloat("./dyos.1.ini","camera","pos.y")
	float 14@ = IniFile.GetFloat("./dyos.1.ini","camera","pos.z")
	float 15@ = IniFile.GetFloat("./dyos.1.ini","camera","see.x")
	float 16@ = IniFile.GetFloat("./dyos.1.ini","camera","see.y")
	float 17@ = IniFile.GetFloat("./dyos.1.ini","camera","see.z")
	Camera.SetVectorMove(12@,13@,14@,12@,13@,14@,10,SwitchType.JumpCut)
	Camera.SetVectorTrack(15@,16@,17@,15@,16@,17@,10,SwitchType.JumpCut)

	{$I ./load/main.txt}

	$numActores = IniFile.GetInt("./dyos.1.ini","numbers","actors")
	if $numActores > 0
	then
		int 5@ = 0
		$indActor = 0

		while $indActor < $numActores
			Wait0()
			5@ += 1
			$indActor += 1

			scene_load_actor()
			float 15@

			call @bset_var 2 B_VAR_INT1 = 15@


			int 0@ = B_ACTOR_TYPE
			int 1@ = $indActor
			int 2@ = 10@
			bset_actor()

			if 10@ == 23
			then
				int 9@ = 8@ // model
				9@ /= 10

				int 0@ = B_ACTOR_MODEL_SPECIAL
				int 1@ = $indActor
				int 2@ = 9@
				bset_actor()

				while 8@ >= 10 // type
					8@ -= 10
				end

				int 0@ = B_ACTOR_GENERATION
				int 1@ = $indActor
				int 2@ = 8@
				bset_actor()

				int 15@ = 9@

				if or
					8@ == GENERATION_SPECIAL
					8@ == GENERATION_ROPE_SPECIAL
				then LoadSpecialActor()
				end

				if or
					8@ == GENERATION_CUSTOM
					8@ == GENERATION_ROPE_CUSTOM
				then CustomSkin()
				end

				repeat
					Wait0()
				until SpecialActor.Available(1)

				if or
					8@ == GENERATION_SPECIAL
					8@ == GENERATION_CUSTOM
				then $Actor($indActor,255i) = Actor.Create(PedType.Special, #SPECIAL01, 11@, 12@, 13@)
				end
				if or
					8@ == GENERATION_ROPE_SPECIAL
					8@ == GENERATION_ROPE_CUSTOM
				then $Actor($indActor,255i) = Actor.CreateOnRope(PedType.Special, #SPECIAL01, 11@, 12@, 13@)
				end

				SpecialActor.Release(1)
			else
				while 8@ >= 10 // type
					8@ -= 10
				end

				if 8@ == GENERATION_RANDOM
				then $Actor($indActor,255i) = Actor.CreateRandom(11@, 12@, 13@)
				else
					Model.Load(9@)
					repeat
						Wait0()
					until Model.Available(9@)
					if 8@ == GENERATION_ROPE_NORMAL
					then $Actor($indActor,255i) = Actor.CreateOnRope(10@, 9@, 11@, 12@, 13@)
					else $Actor($indActor,255i) = Actor.Create(10@, 9@, 11@, 12@, 13@)
					end
					
					int 0@ = B_ACTOR_GENERATION
					int 1@ = $indActor
					int 2@ = 8@
					bset_actor()
					Model.Release(9@)
				end
			end
			Actor.Angle($Actor($indActor,255i)) = 14@
			
			Actor.SetKeepPosition($Actor($indActor,255i), true)
			Actor.SetCollisionDetection($Actor($indActor,255i), false)

			if or
				8@ == GENERATION_ROPE_NORMAL
				8@ == GENERATION_ROPE_SPECIAL
				8@ == GENERATION_ROPE_CUSTOM
			then
				float 11@
				float 12@
				float 13@
				float 14@
				float 15@ = 11@
				float 16@ = 12@
				float 17@ = 13@
				float 18@ = 14@
			else
				0@ = B_VAR_INT1
				bget_var()
				float 0@
				float 15@ = 0@
			end

			int 0@ = B_ACTOR_LCOORD_X
			int 1@ = $indActor
			float 2@ = 15@ // x
			bset_actor()

			int 0@ = B_ACTOR_LCOORD_Y
			int 1@ = $indActor
			float 2@ = 16@ // y
			bset_actor()

			int 0@ = B_ACTOR_LCOORD_Z
			int 1@ = $indActor
			float 2@ = 17@ // z
			bset_actor() 

			int 0@ = B_ACTOR_LCOORD_A
			int 1@ = $indActor
			float 2@ = 18@ // a
			bset_actor()

			if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_BULLETS // bit 1 == false
			then 10@ = false
			else 10@ = true
			end
			if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_FIRE // bit 1 == false
			then 11@ = false
			else 11@ = true
			end
			if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_EXPLOSION // bit 1 == false
			then 12@ = false
			else 12@ = true
			end
			if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_COLLISION // bit 1 == false
			then 13@ = false
			else 13@ = true
			end
			if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_IMMUNITION_MELEE // bit 1 == false
			then 14@ = false
			else 14@ = true
			end
			if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_DROWN_AT_WATER // bit 1 == false
			then 15@ = false
			else 15@ = true
			end
			Actor.SetImmunities($Actor($indActor,255i),10@,11@,12@,13@,14@)
			Actor.SetDrownAtWater($Actor($indActor,255i),15@)

			if 08B4: $statsActor($indActor,255i) is_true ACTOR_PROP_BLEEDING // bit 1 == true
			then Actor.SetBleeding($Actor($indActor,255i), true)
			end

			if 08B4: $statsActor($indActor,255i) is_true ACTOR_PROP_VISION // is TRUE
			then Actor.SetVisibility($Actor($indActor,255i), true)
			end

			/*
				dividendo = 100000
				divisor = 1000
				cociente = Math.floor(dividendo / divisor)
				resto = dividendo - (cociente * divisor)
			*/

			int 20@

			float 0@ = Math.IntToFloat_LsL(20@)
				  0@ /= 1000.0
			int   0@ = Math.FloatToInt_LsL(0@)

			int   1@ = 20@
			int	  2@ = 0@
				  2@ *= 1000
				  1@ -= 2@

			Actor.SetHealth($Actor($indActor,255i), 0@)
			Actor.SetMaxHealth($Actor($indActor,255i),0@)
			Actor.SetAddArmour($Actor($indActor,255i), 1@)


			Actor.SetMeleeAccuracy($Actor($indActor,255i),23@)
			int 0@ = B_ACTOR_ACU_MLLE
			int 1@ = $indActor
			float 2@ = 23@
			bset_actor()
			Actor.SetWeaponAccuracy($Actor($indActor,255i),24@)
			int 0@ = B_ACTOR_ACU_WPON
			int 1@ = $indActor
			int 2@ = 24@
			bset_actor()
			Actor.SetAttackRate($Actor($indActor,255i),25@)
			int 0@ = B_ACTOR_ACU_DIST
			int 1@ = $indActor
			int 2@ = 25@
			bset_actor()

			if 26@ >= 0
			then
				int $CURRENT_ANIM_ID = 26@
				int 2@ = $CURRENT_ANIM_ID
				if $CURRENT_ANIM_ID >= 500
				then
					$CURRENT_ANIM_ID -= 500
					AnimDyos()
				else AnimDyom()
				end
				
				if $ANIM_NOT_IS_OF_PED == false
				then
					Task.PlayAnim($Actor($indActor,255i), v$IFP_ANIM, "PED", 30.0, true, false, false, false, -1)
				else
					Animation.Load(v$IFP_FILE)
					repeat
						Wait0()
					until Animation.Available(v$IFP_FILE)
					Task.PlayAnim($Actor($indActor,255i), v$IFP_ANIM, v$IFP_FILE, 30.0, true, false, false, false, -1)
					Animation.Remove(v$IFP_FILE)
				end

				08BA: $statsActor($indActor,255i) set_true ACTOR_PROP_PLAY_ANIM_DYOM // bit 1 = true
				int 0@ = B_ACTOR_REP_ANIM
				int 1@ = $indActor
     int 2@ = $CURRENT_ANIM_ID
				bset_actor()
			else
				08C0: $statsActor($indActor,255i) set_false ACTOR_PROP_PLAY_ANIM_DYOM // bit 1 = false

				int 0@ = B_ACTOR_REP_ANIM
				int 1@ = $indActor
				int 2@ = -1
				bset_actor()
			end

			10@ = IniFile.GetInt("./dyos.1.ini","game","set.interior")
			Actor.SetLinkInInterior($Actor($indActor,255i), 10@)
		end
	end

	$numAutos = IniFile.GetInt("./dyos.1.ini","numbers","vehicles")
	if $numAutos > 0
	then
		int 5@ = 0
		$indAuto = 0

		while $indAuto < $numAutos
			Wait0()
			5@ += 1
			$indAuto += 1

			{$I ./load/vehicle.txt}

			Model.Load(12@)
			repeat
				Wait0()
			until Model.Available(12@)

			$Auto($indAuto,255i) = Car.Create(12@, 13@, 14@, 15@)
			Model.Release(12@)
			Car.SetQuaternion($Auto($indAuto,255i),16@,17@,18@,19@)

			float 20@ // w
			float 21@ // x
			float 22@ // y
			float 23@ // z


			if bit_is_false $statsAuto($indAuto,255i) AUTO_PROP_COLISION
			then 
				Car.SetKeepPosition($Auto($indAuto,255i), true)
				Car.SetCollision($Auto($indAuto,255i), false)
			end

			if bit_is_false $statsAuto($indAuto,255i) AUTO_PROP_VISION
			then Car.SetVisibility($Auto($indAuto,255i), false)
			end

			if bit_is_true $statsAuto($indAuto,255i) AUTO_PROP_LIGHTS
			then 
				if not Car.IsEngineOn($Auto($indAuto,255i))
				then Car.SetEngineOnB($Auto($indAuto,255i), true)
				end
				Car.SetLights($Auto($indAuto,255i), CarLightMode.On)
			end

			6@ = IniFile.GetInt("./dyos.1.ini","game","set.interior")
			Car.SetLinkInInterior($Auto($indAuto,255i), 6@)


			if bit_is_true $statsAuto($indAuto,255i) AUTO_PROP_SIREN
			then
				if Car.IsEmergencyVehicle($Auto($indAuto,255i))
				then
					if Car.IsSirenOn($Auto($indAuto,255i))
					then Car.SetOnSiren($Auto($indAuto,255i), true)
					end
				else
					24@ = Car.GetModel($Auto($indAuto,255i))
					if or
						24@ == #TAXI
						24@ == #CABBIE
					then
						Car.SetTaxiLight($Auto($indAuto,255i), true)
					else
						if $ModoDeCompilacion == f_PC
						then
							if Fs.isFileExist("./CLEO/CLEO+.cleo")
							then Car.SetAlarm($Auto($indAuto,255i), 2)
							end
						end
					end
				end
			end

			Car.SetHealth($Auto($indAuto,255i), 25@)

			if 88B4: $statsAuto($indAuto,255i) is_false AUTO_PROP_IMMUNITION_BULLETS // bit 1 == false
			then 10@ = false
			else 10@ = true
			end
			if 88B4: $statsAuto($indAuto,255i) is_false AUTO_PROP_IMMUNITION_FIRE // bit 1 == false
			then 11@ = false
			else 11@ = true
			end
			if 88B4: $statsAuto($indAuto,255i) is_false AUTO_PROP_IMMUNITION_EXPLOSION // bit 1 == false
			then 12@ = false
			else 12@ = true
			end
			if 88B4: $statsAuto($indAuto,255i) is_false AUTO_PROP_IMMUNITION_COLLISION // bit 1 == false
			then 13@ = false
			else 13@ = true
			end
			if 88B4: $statsAuto($indAuto,255i) is_false AUTO_PROP_IMMUNITION_MELEE // bit 1 == false
			then 14@ = false
			else 14@ = true
			end
			Car.SetImmunities($Auto($indAuto,255i),10@,11@,12@,13@,14@)

			if 26@ >= 0
			then Car.SetPaintjob($Auto($indAuto,255i),26@)
			end
			Car.SetColorA($Auto($indAuto,255i),27@,28@)
			if 29@ >= 1
			then Car.SetColorB($Auto($indAuto,255i),29@,30@)
			end
		end
	end

	$numObjetos = IniFile.GetInt("./dyos.1.ini","numbers","objects")
	if $numObjetos > 0
	then
		int 5@ = 0
		$indObjeto = 0

		while $indObjeto < $numObjetos
			Wait0()
			5@ += 1
			$indObjeto += 1

			scene_load_object()

			Model.Load(6@)
			repeat
				Wait0()
			until Model.Available(6@)
			$Objeto($indObjeto,255i) = Object.Create(6@, 0.0, 0.0, 0.0)
			Model.Release(6@)
			Object.SetPosition($Objeto($indObjeto,255i), 7@, 8@, 9@)
			Object.SetQuaternion($Objeto($indObjeto,255i),10@,11@,12@,13@)

			if bit_is_false $statsObjet($indObjeto,255i) OBJETO_PROP_COLISION
			then Object.SetCollisionDetection($Objeto($indObjeto,255i), false)
			end
			
			if bit_is_false $statsObjet($indObjeto,255i) OBJETO_PROP_VISION
			then Object.SetVisibility($Objeto($indObjeto,255i), false)
			end

			6@ = IniFile.GetInt("./dyos.1.ini","game","set.interior")
			Object.SetLinkInInterior($Objeto($indObjeto,255i), 6@)
			
		end
	end
	
	if $numActores > 0
	then
		int 5@ = 0
		$indActor = 0

		while $indActor < $numActores
			Wait0()
			5@ += 1
			$indActor += 1

			scene_load_actor()

			int 0@ = B_ACTOR_GENERATION
			int 1@ = $indActor
			bget_actor()

			if or
				0@ == GENERATION_ROPE_NORMAL
				0@ == GENERATION_ROPE_SPECIAL
				0@ == GENERATION_ROPE_CUSTOM
				0@ == GENERATION_ROPE_RANDOM
			then
				Actor.SetKeepPosition($Actor($indActor,255i), false)
				Actor.SetCollisionDetection($Actor($indActor,255i), true)

				08BA: $statsActor($indActor,255i) set_true ACTOR_PROP_COLISION // set TRUE
			else
				if 08B4: $statsActor($indActor,255i) is_true ACTOR_PROP_COLISION // is TRUE
				then
					Actor.SetKeepPosition($Actor($indActor,255i), false)
					Actor.SetCollisionDetection($Actor($indActor,255i), true)

					if 88B4: $statsActor($indActor,255i) is_false ACTOR_PROP_FREEZE_POSITION
					then Actor.SetLockInCurrentPosition($Actor($indActor,255i), false)
					else Actor.SetLockInCurrentPosition($Actor($indActor,255i), true)
					end
				else
					Actor.SetKeepPosition($Actor($indActor,255i), true)
					Actor.SetCollisionDetection($Actor($indActor,255i), false)
				end
			end

			if bit_is_true $statsActor($indActor,255i) ACTOR_PROP_ATTACHED
			then
				int 21@

				int 0@ = B_ACTOR_ATTACHED_TO
				int 1@ = $indActor
				int 2@ = 21@
				bset_actor()

				// attached
				int 7@ = 21@
					7@ /= 100000

				// entity
				int 28@ = 21@
					30@ = 100000
				Math_Module()
				1@ /= 10000
				int 6@ = 1@

				// coords
				int 28@ = 21@
					30@ = 1000
					Math_Module()
				    1@ /= 100
				int 8@ = 1@

				// bone
				int 28@ = 21@
					30@ = 10
					Math_Module()
				int 9@ = 1@

				if 7@ == ATTACHED_TO_CAR
				then
					$indAuto = 6@
					if 8@ == ATTACHED_GLOBAL
					then Actor.SetPositionIntoTurretOnCarAtOffset($Actor($indActor,255i), $Auto($indAuto,255i), 15@, 16@, 17@, 0, 18@, 0)
					end
					if 8@ == ATTACHED_LOCAL
					then
						//ATTACHED_AT_CHASIS
						//ATTACHED_AT_LFRONT
						//ATTACHED_AT_RFRONT
						//ATTACHED_AT_LREAR
						//ATTACHED_AT_RREAR
						//ATTACHED_AT_LMID
						//ATTACHED_AT_RMID
						if 9@ == ATTACHED_AT_LFRONT
						then Actor.SetPositionInCarDriver($Actor($indActor,255i), $Auto($indAuto,255i))
						end
						if 9@ == ATTACHED_AT_RFRONT
						then Actor.SetPositionInCarPassenger($Actor($indActor,255i), $Auto($indAuto,255i), CarPassSeat.CoDriver)
						end
						if 9@ == ATTACHED_AT_LREAR
						then Actor.SetPositionInCarPassenger($Actor($indActor,255i), $Auto($indAuto,255i), CarPassSeat.RearLeft)
						end
						if 9@ == ATTACHED_AT_RREAR
						then Actor.SetPositionInCarPassenger($Actor($indActor,255i), $Auto($indAuto,255i), CarPassSeat.RearRight)
						end
					end
				end
				if 7@ == ATTACHED_TO_OBJECT
				then
					$indObjeto = 6@
					if 8@ == ATTACHED_GLOBAL
					then Actor.SetPositionIntoTurretOnObject($Actor($indActor,255i), $Objeto($indObjeto,255i), 15@, 16@, 17@, 0, 18@, 0)
					end
				end
			end

			if 22@ > 0 // weapon
			then
				Weapon_GetModelID()

				Model.Load(0@)
				repeat
					Wait0()
				until Model.Available(0@)
				01B2: give_actor $Actor($indActor,255i) weapon 22@ ammo 9999
				Model.Release(0@)
			end
		end
	end
	
	if $numObjetos > 0
	then
		int 5@ = 0
		$indObjeto = 0

		while $indObjeto < $numObjetos
			Wait0()
			5@ += 1
			$indObjeto += 1
			
			scene_load_object()

			if bit_run $statsObjet($indObjeto,255i) OBJETO_PROP_ATTACHED
			then
				int 15@
				int 0@ = B_OBJETO_ATTACHED_TO
				int 1@ = $indObjeto
				int 2@ = 15@
				bset_object()

				// [x][xx][xx][x] = [attached][entity][coords][bone]

				
				// attached
				int 7@ = 15@
					7@ /= 100000

				// entity
				int 28@ = 15@
					30@ = 100000
				Math_Module()
				1@ /= 10000
				int 6@ = 1@

				// coords
				int 28@ = 15@
					30@ = 1000
					Math_Module()
				    1@ /= 100
				int 8@ = 1@

				// bone
				int 28@ = 15@
					30@ = 10
					Math_Module()
				int 9@ = 1@

				if 7@ == ATTACHED_TO_ACTOR
				then
					$indActor = 6@
					if 8@ == ATTACHED_GLOBAL
					then Object.SetAttachToActor($Objeto($indObjeto,255i), $Actor($indActor,255i), 16@, 17@, 18@, 19@, 20@, 21@)
					end
					if 8@ == ATTACHED_LOCAL1
					then Task.PickUpObject($Actor($indActor,255i), $Objeto($indObjeto,255i), 16@, 17@, 18@, 9@, 16, "NULL", "NULL", -1)
					end
					if 8@ == ATTACHED_LOCAL2
					then Task.PickUpObjectB($Actor($indActor,255i), $Objeto($indObjeto,255i), 16@, 17@, 18@, 9@, 16, "NULL", "NULL", -1)
					end
				end
				if 7@ == ATTACHED_TO_PLAYER
				then
					$indActor = 6@
					if 8@ == ATTACHED_GLOBAL
					then Object.SetAttachToActor($Objeto($indObjeto,255i), _Player, 16@, 17@, 18@, 19@, 20@, 21@)
					end
					if 8@ == ATTACHED_LOCAL1
					then Task.PickUpObject(_Player, $Objeto($indObjeto,255i), 16@, 17@, 18@, 9@, 16, "NULL", "NULL", -1)
					end
					if 8@ == ATTACHED_LOCAL2
					then Task.PickUpObjectB(_Player, $Objeto($indObjeto,255i), 16@, 17@, 18@, 9@, 16, "NULL", "NULL", -1)
					end
				end
				if 7@ == ATTACHED_TO_CAR
				then
					$indAuto = 6@
					if 8@ == ATTACHED_GLOBAL
					then Object.SetAttachToCar($Objeto($indObjeto,255i), $Auto($indAuto,255i), 16@, 17@, 18@, 19@, 20@, 21@)
					end
				end

			end// OBJETO_PROP_ATTACHED


			if bit_is_true $statsObjet($indObjeto,255i) OBJETO_PROP_HAVE_PARTICLE
			then
				int 14@
				int 10@

				int 6@ = 14@
				ParticleIDs()
				Particle.Play(10@)

				0@ = PARTICLE_HANDLE
				int 1@ = $indObjeto
				int 2@ = 10@
				bset_particle()

				0@ = PARTICLE_ID
				int 1@ = $indObjeto
				int 2@ = 6@
				bset_particle()
			end
		end
	end

	float 12@ = IniFile.GetFloat("./dyos.1.ini","camera","pos.x")
	float 13@ = IniFile.GetFloat("./dyos.1.ini","camera","pos.y")
	float 14@ = IniFile.GetFloat("./dyos.1.ini","camera","pos.z")
	float 15@ = IniFile.GetFloat("./dyos.1.ini","camera","see.x")
	float 16@ = IniFile.GetFloat("./dyos.1.ini","camera","see.y")
	float 17@ = IniFile.GetFloat("./dyos.1.ini","camera","see.z")
	Camera.SetVectorMove(12@,13@,14@,12@,13@,14@,20,SwitchType.JumpCut)
	Camera.SetVectorTrack(15@,16@,17@,15@,16@,17@,20,SwitchType.JumpCut)

	18@ = IniFile.GetInt("./dyos.1.ini","camera","set.see.ent")
	19@ = IniFile.GetInt("./dyos.1.ini","camera","set.see.num")
	if $ModoDeCompilacion == f_PC
	then
		set_cleo_shared_var CAMARA_MIRAR = 18@
		set_cleo_shared_var CAMARA_MIRAR_ENTIDAD = 19@
	else
		Android.SetVar(CAMARA_MIRAR,18@)
		Android.SetVar(CAMARA_MIRAR_ENTIDAD,19@)
	end
	Wait0()
else Text.PrintHelp('M00001C')
end
//fade 1 0
jump @CerrarElMenu