:scm_read_command
// FILE_VERSION [XXX][X][XX]
//
// [XXX] : INT version (distribution.version.parch)
// [X] : BOOL (0-alpha or 1-beta)
// [XX] : prerelease

if FILE_VERSION == 300010 // DYOS 3.0.0-alpha.10
then
  if FILE_COMMAND >= 0x8000
  then SCM_IF_ACTIVE = 1
  end

  switch FILE_COMMAND
    // AREAS IDs
    // 0x0000 - 0x0FFF : Command simple
    // 0x8000 - 0x8FFF : Command conditional

//    if FILE_COMMAND >= 0x8000
//    then
//        SCM_COMMAND_IS_CONDITIONAL = true
//        if SCM_ENTER_CONDITION == false
//        then
//            SCM_ENTER_CONDITION = true
//            SCM_COND_START_OFFSET = SCM_CURRENT_OFFSET
//        end
//    else
//        SCM_COMMAND_IS_CONDITIONAL = false
//        SCM_ENTER_CONDITION = false
//    end

// 0x0000 : Control flow

    case VM_WAIT

        read_buffer_Value()
        call @waitPlis 2 0@ false
    
    case VM_GOTO

        read_buffer_Value()
        SCM_CURRENT_OFFSET = 0@

    case VM_IF

        read_buffer_Value()
        SCM_IF_TYPE = 0@        // 0 = AND, 1 = OR
        read_buffer_Value()
        SCM_IF_FALSE_JUMP = 0@  // false jump

        // Resolver IF
        if SCM_IF_ACTIVE == 1
        then
            if SCM_IF_TYPE == 0 // AND
            then
                if SCM_CONT_CONDITION <> SCM_IF_TRUE
                then
                    SCM_CURRENT_OFFSET = SCM_IF_FALSE_JUMP
                end
            else // OR
                if SCM_CONT_CONDITION == 0
                then
                    SCM_CURRENT_OFFSET = SCM_IF_FALSE_JUMP
                end
            end
        end

        SCM_IF_ACTIVE = 0
        SCM_IF_TRUE = 0
        SCM_CONT_CONDITION = 0

    case VM_END_THREAD
    
        SCM_CURRENT_OFFSET = FILE_SIZE

    case VM_NOP

        nop

//   0x1000 : Player

    case VM_PLAYER_SET_POS

        read_buffer_Vec3()
        Actor.SetPosition(hPlayer,0@,1@,2@)

    case VM_PLAYER_SET_WANTED

        read_buffer_Value()
        Player.SetWantedLevel(hChar) = 0@

    case VM_PLAYER_SET_WEAPON

        read_buffer_Value()
        //0A9D: read_file FILE_HANDLE size 4 to hPlayer // Actor
        read_buffer_Vec3()
        call @GiveWeaponAndAmmo 4 actor hPlayer model_id 0@ weapon_id 1@ ammo 2@

    case VM_PLAYER_SET_ANIM

        read_buffer_Vec3()

        CURRENT_ANIM_ID = 1@
        if 0@ == PLAYER_ANIM_LIB_DYOM
        then AnimDyom()
        end
        if 0@ == PLAYER_ANIM_LIB_DYOS
        then AnimDyos()
        end
        
        if ANIM_NOT_IS_OF_PED == false
        then
            if lbit_run 2@ PLAYER_PROP_COLISION
            then Task.PlayAnimSecondary(hPlayer, IFP_ANIM, "PED", 30.0, true, false, false, false, -1)
            else Task.PlayAnim(hPlayer, IFP_ANIM, "PED", 30.0, true, false, false, false, -1)
            end
        else
            Animation.Load(IFP_FILE)
            repeat
                Wait0()
            until Animation.Available(IFP_FILE)
            if lbit_run 2@ PLAYER_PROP_COLISION
            then Task.PlayAnimSecondary(hPlayer, IFP_ANIM, IFP_FILE, 4.0, true, false, false, false, -1)
            else Task.PlayAnim(hPlayer, IFP_ANIM, IFP_FILE, 4.0, true, false, false, false, -1)
            end
            Animation.Remove(IFP_FILE)
        end

    case VM_PLAYER_SET_FIGHT

        read_buffer_Value()
        Actor.SetFightStyle(hPlayer, 0@, 6)
        
    case VM_PLAYER_SET_HEALTH

        read_buffer_Value()
        Actor.SetHealth(hPlayer) = 0@

// 0x8000 --------------------

    case VM_PLAYER_AT_POS
        read_buffer_Vec3()
        // 0@ = X
        // 1@ = Y
        // 2@ = Z

        SCM_CONT_CONDITION += 1

        31@ = SCM_CONT_CONDITION * 4
        31@ -= 4

        2401: read_memory_with_offset SCM_BUFFER_BLIPS offset 31@ size 4 store_to 10@
        2401: read_memory_with_offset SCM_BUFFER_SPHERES offset 31@ size 4 store_to 11@

        if 10@ == 0
        then
            10@ = Blip.Create(0@, 1@, 2@, 1, 1)
            0165: set_marker 10@ color_to 0
            0168: show_on_radar 10@ size 3
            018B: show_on_radar 10@ in 3

            11@ = Sphere.Create(0@, 1@, 2@, 2.0)
            2402: write_memory_with_offset SCM_BUFFER_BLIPS offset 31@ size 4 value 10@
            2402: write_memory_with_offset SCM_BUFFER_SPHERES offset 31@ size 4 value 11@
        end

        if Actor.IsLocated3D(hPlayer,1,0@,1@,2@, 1.0, 1.0, 1.0)
        then
            SCM_IF_TRUE += 1

            Blip.Remove(10@)
            Sphere.Remove(11@)
            2402: write_memory_with_offset SCM_BUFFER_BLIPS offset 31@ size 4 value 0
            2402: write_memory_with_offset SCM_BUFFER_SPHERES offset 31@ size 4 value 0
        end

        printf "goto the red circle~n~ %.2f %.2f %.2f" 86 0@ 1@ 2@

//   0x2000 : Actor/Task

    case VM_ACTOR_CREATE

        read_buffer_Value() // type
        10@ = 0@ 
        read_buffer_Value() // model
        ACTOR_MODEL_REQ = 0@
        read_buffer_Vec3() // coords
        12@ = 0@ + 0
        13@ = 1@ + 0
        14@ = 2@ + 0
        read_buffer_Value()  // angle
        tempvar_Angle = 0@ + 0

        Model.Load(ACTOR_MODEL_REQ)
        while not Model.Available(ACTOR_MODEL_REQ)
            call @waitPlis 2 0 false
        end
        
        Actor.Create(SCM_ACTOR, 10@, ACTOR_MODEL_REQ, 12@, 13@, 14@)
        Actor.Angle(SCM_ACTOR) = tempvar_Angle

        31@ = SCM_NUM_ACTORS * 19
        2402: write_memory_with_offset SCM_BUFFER_ACTORS offset 31@ size 4 value SCM_ACTOR

        SCM_NUM_ACTORS += 1

    default
        nop
        SCM_CURRENT_OFFSET -= 4
        printf "ERROR:~n~UNKNOW COMMAND: %x~n~OFFSET: %i" 2000 FILE_COMMAND SCM_CURRENT_OFFSET
        call @waitPlis 2 2000 0
        SCM_CURRENT_OFFSET = FILE_SIZE
  end
end
return