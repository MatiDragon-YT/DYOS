:CrearActor
	FrenarSeleccion()
	int 0@ = 1
	bget_var()
	int 6@ = 0@

	$indActor = $numActores
	if $numActores < 255
	then
		$numActores++
		$indActor++
		GenerarActor()
		EsconderMenu()
		30@ = false
		repeat
			wait 0
			FixUpdate()

			if $ModoDeCompilacion == ePara_PC
			then inputNumberPC()
			end
			Text.PrintWithNumberNow('M000007', 6@, 200, 0)

			if 30@ == false
			then
				if $ModoDeCompilacion == ePara_Android
				then Text.PrintHelp('M0000GB')
				else Text.PrintHelp('M0000GC')
				end
				30@ = true
			end
			if ParaIzquierda()
			then
				TIMERA = 0

				while ParaIzquierda()
					wait 0
					FixUpdate()
					if TIMERA > 300
					then
						6@ -= 9
						break
					end
				end
				6@--
				
				ModeloAnterior_Actor()

				Actor.Remove($Actor($indActor,255i))
				GenerarActor()
				while ParaIzquierda()
					wait 0
					FixUpdate()
				end
			end
			if ParaDerecha()
			then
				TIMERA = 0

				while ParaDerecha()
					wait 0
					FixUpdate()
					if TIMERA > 300
					then
						6@ += 9
						break
					end
				end
				6@++
				
				ModeloSiguiente_Actor()
				Actor.Remove($Actor($indActor,255i))
				GenerarActor()
				while ParaDerecha()
					wait 0
					FixUpdate()
				end
			end
			if ParaAtras()
			then
				6@ -= 10
				
				ModeloAnterior_Actor()
				Actor.Remove($Actor($indActor,255i))
				GenerarActor()
				while ParaAtras()
					wait 0
					FixUpdate()
				end
			end
			if ParaAdelante()
			then
				6@ += 10
			
				ModeloSiguiente_Actor()
				Actor.Remove($Actor($indActor,255i))
				GenerarActor()
				while ParaAdelante()
					wait 0
					FixUpdate()
				end
			end

			param1r = false
			if ParaEspecial()
			then
				6@ = Actor.GetModel(_Player)
				Actor.Remove($Actor($indActor,255i))
				GenerarActor()
				param1r = true
				break
			end
			if Selecionas()
			then
				param1r = true
				if not Actor.Model($Actor($indActor,255i)) == 6@
				then
					Actor.Remove($Actor($indActor,255i))
					GenerarActor()
				end
				break
			end
			if ParaAbajo()
			then
				param1r = undefined
				break
			end
			int 0@ = 1
			int 1@ = 6@
			bset_var()
		until SalirDelMenu()

		AparecerMenu()
		Menu.SetActiveItem($PANEL, 0)
		Text.ClearHelp()
		Text.ClearPrints()
	else
		Text.PrintHelp('M00000M')   
	end
return

:GenerarActor
	Model.Load(6@)
	Text.PrintHelpForever('M0000AW')
	if $ModoDeCompilacion == ePara_PC
	then
		TIMERB = 0
		while TIMERB <= 83
			Wait0()
		end
	else
		0@ = 0
		while 0@ <= 12
			Wait0()
			0@ += 1
		end
	end
	repeat
		Wait0()
	until Model.Available(6@)
	Text.ClearHelp()

	GetUpdatePlayerPosition()
	$Actor($indActor,255i) = Actor.Create(PedType.CivMale, 6@, $tempvar_X_coord, $tempvar_Y_coord, $tempvar_Z_coord)
	Actor.Angle($Actor($indActor,255i)) = $tempvar_Angle
	Actor.SetLinkInInterior($Actor($indActor,255i), $Active_Interior)
	
	Actor.SetKeepPosition($Actor($indActor,255i), true)
	Actor.SetCollisionDetection($Actor($indActor,255i), false)
	
	Model.Release(6@)
	
	$statsActor($indActor,255i) = 0x0
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_COLISION
	bit_set_true $statsActor($indActor,255i) ACTOR_PROP_VISION
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_IMMUNITION_BULLETS
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_IMMUNITION_FIRE
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_IMMUNITION_EXPLOSION
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_IMMUNITION_COLLISION
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_IMMUNITION_MELEE
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_BLEEDING
	bit_set_true $statsActor($indActor,255i) ACTOR_PROP_DROWN_AT_WATER
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_ATTACHED
	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_FREEZE_POSITION

	if Actor.IsCrouching(_Player)
	then
		Task.PlayAnim($Actor($indActor,255i),"WEAPON_crouch", "PED", 4.0, true, false, false, false, -1)
		bit_set_true $statsActor($indActor,255i) ACTOR_PROP_CROUCH
	else
		bit_set_false $statsActor($indActor,255i) ACTOR_PROP_CROUCH 
	end

	bit_set_false $statsActor($indActor,255i) ACTOR_PROP_PLAY_ANIM_DYOM
	int 0@ = B_ACTOR_REP_ANIM
	int 1@ = $indActor
	2@ = -1
	bset_actor()

	int 0@ = B_ACTOR_TYPE
	int 1@ = $indActor
	int 2@ = 4
	bset_actor()

	int 0@ = B_ACTOR_ACU_MLLE
	int 1@ = $indActor
	float 2@ = 50.0
	bset_actor()
	Actor.SetMeleeAccuracy($Actor($indActor,255i),50.0)

	int 0@ = B_ACTOR_ACU_WPON
	int 1@ = $indActor
	int 2@ = 50
	bset_actor()
	Actor.SetWeaponAccuracy($Actor($indActor,255i),50)

	int 0@ = B_ACTOR_ACU_DIST
	int 1@ = $indActor
	int 2@ = 50
	bset_actor()
	Actor.SetAttackRate($Actor($indActor,255i),50)

	//$attActor($indActor,255i) = false

	//$attXActor($indActor,255f) = 0.0
	//$attYActor($indActor,255f) = 0.0
	//$attZActor($indActor,255f) = 0.0
	//$attAActor($indActor,255f) = 0.0

	Text.PrintWithNumberNow('M000007', 6@, 1000, 0)
return


:ModeloAnterior_Actor
	if 6@ < 0
	then 6@ = 288
	end
	if and
		6@ < 7
		6@ > 0
	then 6@ = 0 
	end   
	if 6@ == 8
	then 6@ = 7
	end 
	if 6@ == 42
	then 6@ = 41
	end 
	if 6@ == 65
	then 6@ = 64
	end 
	if 6@ == 74
	then 6@ = 73
	end 
	if 6@ == 86
	then 6@ = 85
	end
	if 6@ == 119
	then 6@ = 118
	end 
	if 6@ == 149
	then 6@ = 148
	end  
	if 6@ == 208
	then 6@ = 207
	end
	if and
		6@ < 274
		6@ > 264
	then 6@ = 264
	end
return

:ModeloSiguiente_Actor
	if and
		6@ > 0
		6@ < 7
	then 6@ = 7
	end
	if 6@ == 8
	then 6@ = 9
	end
	if 6@ == 42
	then 6@ = 43
	end
	if 6@ == 65
	then 6@ = 66
	end
	if 6@ == 74
	then 6@ = 75
	end
	if 6@ == 86
	then 6@ = 87
	end
	if 6@ == 119
	then 6@ = 120
	end
	if 6@ == 149
	then 6@ = 150
	end
	if 6@ == 208
	then 6@ = 209
	end
	if and
		6@ > 264
		6@ < 274
	then 6@ = 274
	end 
	if 6@ > 288
	then 6@ = 0
	end 
return